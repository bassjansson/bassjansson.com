<!DOCTYPE html>
<html>
    <head>
        <title>Bass Jansson</title>
        <meta http-equiv="Content-Type" content="text/html" charset="utf-8">

        <!-- Include utils -->
        <script src="scripts/utils/sylvester.js"    type="text/javascript"></script>
        <script src="scripts/utils/gl-utils.js"     type="text/javascript"></script>
        <script src="scripts/utils/math.js"         type="text/javascript"></script>

        <!-- Include stylesheets -->
        <link href="styles/style.css?v=0.0.6"       type="text/css" rel="stylesheet">

        <!-- Include scripts -->
        <script src="scripts/home.js?v=0.0.6"       type="text/javascript"></script>
        <script src="scripts/webgl.js?v=0.0.6"      type="text/javascript"></script>
        <script src="scripts/polynomial.js?v=0.0.6" type="text/javascript"></script>

        <!-- Vertex shader program -->
        <script id="shader-vs" type="x-shader/x-vertex">

            attribute vec3 aVertexPosition;

            uniform mat4 uMVMatrix;
            uniform mat4 uPMatrix;

            varying mediump vec2 vPosition;

            void main(void)
            {
                gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
                vPosition = vec2(aVertexPosition.x, aVertexPosition.y);
            }

        </script>

        <!-- Fragment shader program -->
        <script id="shader-fs" type="x-shader/x-fragment">

            #define POL_SIZE (5)
            #define DER_SIZE (POL_SIZE - 1)

            #define ITERATIONS 10

            precision mediump float;

            varying vec2 vPosition;

            uniform vec2 uPolynomial[POL_SIZE];
            uniform vec2 uDerivative[DER_SIZE];

            vec2 cMul(vec2 a, vec2 b)
            {
                vec2 c;
                c.x = a.x * b.x - a.y * b.y;
                c.y = a.x * b.y + a.y * b.x;
                return c;
            }

            vec2 cDiv(vec2 a, vec2 b)
            {
                vec2 c;
                float d = b.x * b.x + b.y * b.y;
                c.x = (a.x * b.x + a.y * b.y) / d;
                c.y = (a.y * b.x - a.x * b.y) / d;
                return c;
            }

            float cAbs(vec2 c)
            {
                return sqrt(c.x*c.x + c.y*c.y);
            }

            float cArg(vec2 c)
            {
                return abs(atan(c.y, c.x) * 0.3183098862); // 1/pi
            }

            vec3 hsv2rgb(vec3 c)
            {
                vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
                vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);

                return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
            }

            vec2 polynomial(vec2 z)
            {
                vec2 x = vec2(1, 0);
                vec2 y = vec2(0, 0);

                for (int i = POL_SIZE - 1; i >= 0; --i)
                {
                    y += cMul(x, uPolynomial[i]);
                    x  = cMul(x, z);
                }

                return y;
            }

            vec2 derivative(vec2 z)
            {
                vec2 x = vec2(1, 0);
                vec2 y = vec2(0, 0);

                for (int i = DER_SIZE - 1; i >= 0; --i)
                {
                    y += cMul(x, uDerivative[i]);
                    x  = cMul(x, z);
                }

                return y;
            }

            void main(void)
            {
                vec2 z = vPosition;
                vec2 a = vec2(-0.6172, -2.2738);

                float value = 1.0;

                for (int i = 0; i < ITERATIONS; ++i)
                {
                    z = z - cMul(a, cDiv(polynomial(z), derivative(z)));

                    if (cAbs(z) > 2.0)
                    {
                        value = float(i) / float(ITERATIONS);
                        break;
                    }
                }

                float zVal = value * (value * 0.5 + 0.5) * 0.75;
                float zHue = 0.6 - cArg(z) * 0.3 - cAbs(z) * 0.01;

                gl_FragColor = vec4(hsv2rgb(vec3(zHue, 1.0, zVal)), 1.0);
            }

        </script>
    </head>

    <body onload="start()">
        <canvas id="glCanvas" width=512 height=1024>
            Your browser doesn't appear to support the <code>&lt;canvas&gt;</code> element.
        </canvas>

        <canvas id="mainCanvas" width=2048 height=4096></canvas>

        <script>

            initHome();

            // Wait for our font to load
            setTimeout(function(){ animateHome(); }, 1000);

        </script>

        <!-- <img src="images/basslive.jpg" alt="BassLive 2.0" height="200" width="300"> -->
    </body>
</html>
